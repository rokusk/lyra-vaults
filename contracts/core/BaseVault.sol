// SPDX-License-Identifier: MIT
pragma solidity ^0.7.0;
pragma abicoder v2;

import {SafeMath} from "@openzeppelin/contracts/math/SafeMath.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/SafeERC20.sol";
import {ReentrancyGuard} from "@openzeppelin/contracts/utils/ReentrancyGuard.sol";
import {Ownable} from "@openzeppelin/contracts/access/Ownable.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {Initializable} from "@openzeppelin/contracts/proxy/Initializable.sol";

import {Vault} from "../libraries/Vault.sol";
import {VaultLifecycle} from "../libraries/VaultLifecycle.sol";
import {ShareMath} from "../libraries/ShareMath.sol";

import "hardhat/console.sol";

contract BaseVault is ReentrancyGuard, Ownable, ERC20, Initializable {
  using SafeERC20 for IERC20;
  using SafeMath for uint;
  using ShareMath for Vault.DepositReceipt;

  /************************************************
   *  NON UPGRADEABLE STORAGE
   ***********************************************/

  /// @notice Stores the user's pending deposit for the round
  mapping(address => Vault.DepositReceipt) public depositReceipts;

  /// @notice On every round's close, the pricePerShare value of an rTHETA token is stored
  /// This is used to determine the number of shares to be returned
  /// to a user with their DepositReceipt.depositAmount
  mapping(uint => uint) public roundPricePerShare;

  /// @notice Stores pending user withdrawals
  mapping(address => Vault.Withdrawal) public withdrawals;

  /// @notice Vault's parameters like cap, decimals
  Vault.VaultParams public vaultParams;

  /// @notice Vault's lifecycle state like round and locked amounts
  Vault.VaultState public vaultState;

  /// @notice Fee recipient for the performance and management fees
  address public feeRecipient;

  /// @notice Performance fee charged on premiums earned in rollToNextOption. Only charged when there is no loss.
  uint public performanceFee;

  /// @notice Management fee charged on entire AUM in rollToNextOption. Only charged when there is no loss.
  uint public managementFee;

  // Gap is left to avoid storage collisions. Though RibbonVault is not upgradeable, we add this as a safety measure.
  uint[30] private ____gap;

  // *IMPORTANT* NO NEW STORAGE VARIABLES SHOULD BE ADDED HERE
  // This is to prevent storage collisions. All storage variables should be appended to RibbonThetaVaultStorage
  // or RibbonDeltaVaultStorage instead. Read this documentation to learn more:
  // https://docs.openzeppelin.com/upgrades-plugins/1.x/writing-upgradeable#modifying-your-contracts

  /************************************************
   *  IMMUTABLES & CONSTANTS
   ***********************************************/

  // Round per year scaled up FEE_MULTIPLIER
  uint private immutable roundPerYear;

  /************************************************
   *  EVENTS
   ***********************************************/

  event Deposit(address indexed account, uint amount, uint round);

  event InitiateWithdraw(address indexed account, uint shares, uint round);

  event Redeem(address indexed account, uint share, uint round);

  event ManagementFeeSet(uint managementFee, uint newManagementFee);

  event PerformanceFeeSet(uint performanceFee, uint newPerformanceFee);

  event CapSet(uint oldCap, uint newCap, address manager);

  event Withdraw(address indexed account, uint amount, uint shares);

  event CollectVaultFees(uint performanceFee, uint vaultFee, uint round, address indexed feeRecipient);

  /************************************************
   *  CONSTRUCTOR & INITIALIZATION
   ***********************************************/

  /**
   * @notice Initializes the contract with immutable variables
   */
  constructor(
    address _feeRecipient,
    uint _roundDuration,
    string memory _tokenName,
    string memory _tokenSymbol,
    Vault.VaultParams memory _vaultParams
  ) ERC20(_tokenName, _tokenSymbol) {
    feeRecipient = _feeRecipient;
    uint _roundPerYear = uint256(365 days).mul(Vault.FEE_MULTIPLIER).div(_roundDuration);
    roundPerYear = _roundPerYear;
    vaultParams = _vaultParams;

    uint assetBalance = IERC20(vaultParams.asset).balanceOf(address(this));
    ShareMath.assertUint104(assetBalance);
    vaultState.lastLockedAmount = uint104(assetBalance);
    vaultState.round = 1;
  }

  /************************************************
   *  SETTERS
   ***********************************************/

  /**
   * @notice Sets the new fee recipient
   * @param newFeeRecipient is the address of the new fee recipient
   */
  function setFeeRecipient(address newFeeRecipient) external onlyOwner {
    require(newFeeRecipient != address(0), "!newFeeRecipient");
    require(newFeeRecipient != feeRecipient, "Must be new feeRecipient");
    feeRecipient = newFeeRecipient;
  }

  /**
   * @notice Sets the management fee for the vault
   * @param newManagementFee is the management fee (6 decimals). ex: 2 * 10 ** 6 = 2%
   */
  function setManagementFee(uint newManagementFee) external onlyOwner {
    require(newManagementFee < 100 * Vault.FEE_MULTIPLIER, "Invalid management fee");

    emit ManagementFeeSet(managementFee, newManagementFee);

    // We are dividing annualized management fee by number of rounds in a year
    managementFee = newManagementFee.mul(Vault.FEE_MULTIPLIER).div(roundPerYear);
  }

  /**
   * @notice Sets the performance fee for the vault
   * @param newPerformanceFee is the performance fee (6 decimals). ex: 20 * 10 ** 6 = 20%
   */
  function setPerformanceFee(uint newPerformanceFee) external onlyOwner {
    require(newPerformanceFee < 100 * Vault.FEE_MULTIPLIER, "Invalid performance fee");

    emit PerformanceFeeSet(performanceFee, newPerformanceFee);

    performanceFee = newPerformanceFee;
  }

  /**
   * @notice Sets a new cap for deposits
   * @param newCap is the new cap for deposits
   */
  function setCap(uint newCap) external onlyOwner {
    require(newCap > 0, "!newCap");

    emit CapSet(vaultParams.cap, newCap, msg.sender);

    ShareMath.assertUint104(newCap);
    vaultParams.cap = uint104(newCap);
  }

  /************************************************
   *  DEPOSIT & WITHDRAWALS
   ***********************************************/

  /**
   * @notice Deposits the `asset` from msg.sender.
   * @param amount is the amount of `asset` to deposit
   */
  function deposit(uint amount) external nonReentrant {
    require(amount > 0, "!amount");

    _depositFor(amount, msg.sender);

    // An approve() by the msg.sender is required beforehand
    IERC20(vaultParams.asset).safeTransferFrom(msg.sender, address(this), amount);
  }

  /**
   * @notice Deposits the `asset` from msg.sender added to `creditor`'s deposit.
   * @notice Used for vault -> vault deposits on the user's behalf
   * @param amount is the amount of `asset` to deposit
   * @param creditor is the address that can claim/withdraw deposited amount
   */
  function depositFor(uint amount, address creditor) external nonReentrant {
    require(amount > 0, "!amount");
    require(creditor != address(0), "!creditor");

    _depositFor(amount, creditor);

    // An approve() by the msg.sender is required beforehand
    IERC20(vaultParams.asset).safeTransferFrom(msg.sender, address(this), amount);
  }

  /**
   * @notice Mints the vault shares to the creditor
   * @param amount is the amount of `asset` deposited
   * @param creditor is the address to receieve the deposit
   */
  function _depositFor(uint amount, address creditor) private {
    uint currentRound = vaultState.round;
    uint totalWithDepositedAmount = totalBalance().add(amount);

    require(totalWithDepositedAmount <= vaultParams.cap, "Exceed cap");

    emit Deposit(creditor, amount, currentRound);

    Vault.DepositReceipt memory depositReceipt = depositReceipts[creditor];

    // process unprocessed pending deposit from the previous rounds
    uint unredeemedShares = depositReceipt.getSharesFromReceipt(
      currentRound,
      roundPricePerShare[depositReceipt.round],
      vaultParams.decimals
    );

    uint depositAmount = amount;

    // If we have a pending deposit in the current round, we add on to the pending deposit
    if (currentRound == depositReceipt.round) {
      uint newAmount = uint(depositReceipt.amount).add(amount);
      depositAmount = newAmount;
    }

    ShareMath.assertUint104(depositAmount);

    depositReceipts[creditor] = Vault.DepositReceipt({
      round: uint16(currentRound),
      amount: uint104(depositAmount),
      unredeemedShares: uint128(unredeemedShares)
    });

    uint newTotalPending = uint(vaultState.totalPending).add(amount);
    ShareMath.assertUint128(newTotalPending);

    vaultState.totalPending = uint128(newTotalPending);
  }

  /**
   * @notice Initiates a withdrawal that can be processed once the round completes
   * @param numShares is the number of shares to withdraw
   */
  function initiateWithdraw(uint numShares) external nonReentrant {
    require(numShares > 0, "!numShares");

    // We do a max redeem before initiating a withdrawal
    // But we check if they must first have unredeemed shares
    if (depositReceipts[msg.sender].amount > 0 || depositReceipts[msg.sender].unredeemedShares > 0) {
      _redeem(0, true);
    }

    // This caches the `round` variable used in shareBalances
    uint currentRound = vaultState.round;
    Vault.Withdrawal storage withdrawal = withdrawals[msg.sender];

    bool withdrawalIsSameRound = withdrawal.round == currentRound;

    emit InitiateWithdraw(msg.sender, numShares, currentRound);

    uint existingShares = uint(withdrawal.shares);

    uint withdrawalShares;
    if (withdrawalIsSameRound) {
      withdrawalShares = existingShares.add(numShares);
    } else {
      require(existingShares == 0, "Existing withdraw");
      withdrawalShares = numShares;
      withdrawals[msg.sender].round = uint16(currentRound);
    }

    ShareMath.assertUint128(withdrawalShares);
    withdrawals[msg.sender].shares = uint128(withdrawalShares);

    uint newQueuedWithdrawShares = uint(vaultState.queuedWithdrawShares).add(numShares);
    ShareMath.assertUint128(newQueuedWithdrawShares);
    vaultState.queuedWithdrawShares = uint128(newQueuedWithdrawShares);

    _transfer(msg.sender, address(this), numShares);
  }

  /**
   * @notice Completes a scheduled withdrawal from a past round. Uses finalized pps for the round
   */
  function completeWithdraw() external nonReentrant {
    Vault.Withdrawal storage withdrawal = withdrawals[msg.sender];

    uint withdrawalShares = withdrawal.shares;
    uint withdrawalRound = withdrawal.round;

    // This checks if there is a withdrawal
    require(withdrawalShares > 0, "Not initiated");

    require(withdrawalRound < vaultState.round, "Round in progress");

    // We leave the round number as non-zero to save on gas for subsequent writes
    withdrawals[msg.sender].shares = 0;
    vaultState.queuedWithdrawShares = uint128(uint(vaultState.queuedWithdrawShares).sub(withdrawalShares));

    uint withdrawAmount = ShareMath.sharesToAsset(
      withdrawalShares,
      roundPricePerShare[withdrawalRound],
      vaultParams.decimals
    );

    emit Withdraw(msg.sender, withdrawAmount, withdrawalShares);

    _burn(address(this), withdrawalShares);

    require(withdrawAmount > 0, "!withdrawAmount");

    _transferAsset(msg.sender, withdrawAmount);
  }

  /**
   * @notice Redeems shares that are owed to the account
   * @param numShares is the number of shares to redeem
   */
  function redeem(uint numShares) external nonReentrant {
    require(numShares > 0, "!numShares");
    _redeem(numShares, false);
  }

  /**
   * @notice Redeems the entire unredeemedShares balance that is owed to the account
   */
  function maxRedeem() external nonReentrant {
    _redeem(0, true);
  }

  /**
   * @notice Redeems shares that are owed to the account
   * @param numShares is the number of shares to redeem, could be 0 when isMax=true
   * @param isMax is flag for when callers do a max redemption
   */
  function _redeem(uint numShares, bool isMax) internal {
    Vault.DepositReceipt memory depositReceipt = depositReceipts[msg.sender];

    // This handles the null case when depositReceipt.round = 0
    // Because we start with round = 1 at `initialize`
    uint currentRound = vaultState.round;

    uint unredeemedShares = depositReceipt.getSharesFromReceipt(
      currentRound,
      roundPricePerShare[depositReceipt.round],
      vaultParams.decimals
    );

    numShares = isMax ? unredeemedShares : numShares;
    if (numShares == 0) {
      return;
    }
    require(numShares <= unredeemedShares, "Exceeds available");

    // If we have a depositReceipt on the same round, BUT we have some unredeemed shares
    // we debit from the unredeemedShares, but leave the amount field intact
    // If the round has past, with no new deposits, we just zero it out for new deposits.
    depositReceipts[msg.sender].amount = depositReceipt.round < currentRound ? 0 : depositReceipt.amount;

    ShareMath.assertUint128(numShares);
    depositReceipts[msg.sender].unredeemedShares = uint128(unredeemedShares.sub(numShares));

    emit Redeem(msg.sender, numShares, depositReceipt.round);

    _transfer(address(this), msg.sender, numShares);
  }

  /************************************************
   *  VAULT OPERATIONS
   ***********************************************/

  /*
   * @notice Helper function that performs most administrative tasks
   * such as setting next option, minting new shares, getting vault fees, etc.
   * @param lastQueuedWithdrawAmount is old queued withdraw amount
   * @return lockedBalance is the new balance used to calculate next option purchase size or collateral size
   * @return queuedWithdrawAmount is the new queued withdraw amount for this round
   */
  function _rollToNextRound(uint lastQueuedWithdrawAmount) internal returns (uint, uint) {
    (uint lockedBalance, uint queuedWithdrawAmount, uint newPricePerShare, uint mintShares) = VaultLifecycle.rollover(
      totalSupply(),
      vaultParams.asset,
      vaultParams.decimals,
      uint(vaultState.totalPending),
      vaultState.queuedWithdrawShares
    );

    // Finalize the pricePerShare at the end of the round
    uint currentRound = vaultState.round;
    roundPricePerShare[currentRound] = newPricePerShare;

    uint withdrawAmountDiff = queuedWithdrawAmount > lastQueuedWithdrawAmount
      ? queuedWithdrawAmount.sub(lastQueuedWithdrawAmount)
      : 0;

    // Take management / performance fee from previous round and deduct
    lockedBalance = lockedBalance.sub(_collectVaultFees(lockedBalance.add(withdrawAmountDiff)));

    // update round info
    vaultState.totalPending = 0;
    vaultState.round = uint16(currentRound + 1);

    _mint(address(this), mintShares);

    return (lockedBalance, queuedWithdrawAmount);
  }

  /*
   * @notice Helper function that transfers management fees and performance fees from previous round.
   * @param pastWeekBalance is the balance we are about to lock for next round
   * @return vaultFee is the fee deducted
   */
  function _collectVaultFees(uint pastWeekBalance) internal returns (uint) {
    (uint performanceFeeInAsset, , uint vaultFee) = VaultLifecycle.getVaultFees(
      vaultState,
      pastWeekBalance,
      performanceFee,
      managementFee
    );

    if (vaultFee > 0) {
      _transferAsset(payable(feeRecipient), vaultFee);
      emit CollectVaultFees(performanceFeeInAsset, vaultFee, vaultState.round, feeRecipient);
    }

    return vaultFee;
  }

  /**
   * @notice Helper function to make either an ETH transfer or ERC20 transfer
   * @param recipient is the receiving address
   * @param amount is the transfer amount
   */
  function _transferAsset(address recipient, uint amount) internal {
    address asset = vaultParams.asset;
    IERC20(asset).safeTransfer(recipient, amount);
  }

  /************************************************
   *  GETTERS
   ***********************************************/

  /**
   * @notice Returns the asset balance held on the vault for the account
   * @param account is the address to lookup balance for
   * @return the amount of `asset` custodied by the vault for the user
   */
  function accountVaultBalance(address account) external view returns (uint) {
    uint _decimals = vaultParams.decimals;
    uint assetPerShare = ShareMath.pricePerShare(totalSupply(), totalBalance(), vaultState.totalPending, _decimals);
    return ShareMath.sharesToAsset(shares(account), assetPerShare, _decimals);
  }

  /**
   * @notice Getter for returning the account's share balance including unredeemed shares
   * @param account is the account to lookup share balance for
   * @return the share balance
   */
  function shares(address account) public view returns (uint) {
    (uint heldByAccount, uint heldByVault) = shareBalances(account);
    return heldByAccount.add(heldByVault);
  }

  /**
   * @notice Getter for returning the account's share balance split between account and vault holdings
   * @param account is the account to lookup share balance for
   * @return heldByAccount is the shares held by account
   * @return heldByVault is the shares held on the vault (unredeemedShares)
   */
  function shareBalances(address account) public view returns (uint heldByAccount, uint heldByVault) {
    Vault.DepositReceipt memory depositReceipt = depositReceipts[account];

    if (depositReceipt.round == 0) {
      return (balanceOf(account), 0);
    }

    uint unredeemedShares = depositReceipt.getSharesFromReceipt(
      vaultState.round,
      roundPricePerShare[depositReceipt.round],
      vaultParams.decimals
    );

    return (balanceOf(account), unredeemedShares);
  }

  /**
   * @notice The price of a unit of share denominated in the `asset`
   */
  function pricePerShare() external view returns (uint) {
    return ShareMath.pricePerShare(totalSupply(), totalBalance(), vaultState.totalPending, vaultParams.decimals);
  }

  /**
   * @notice Returns the vault's total balance, including the amounts locked into a short position
   * @return total balance of the vault, including the amounts locked in third party protocols
   */
  function totalBalance() public view returns (uint) {
    return uint(vaultState.lockedAmount).add(IERC20(vaultParams.asset).balanceOf(address(this)));
  }

  /**
   * @notice Returns the token decimals
   */
  function decimals() public view override returns (uint8) {
    return vaultParams.decimals;
  }
}
